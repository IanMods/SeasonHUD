plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
    id 'net.darkhax.curseforgegradle' version '1.1.16'
    id "com.modrinth.minotaur" version "2.+"
}

version = mod_version
group = mod_group

base {
    archivesName = mod_id + "-fabric-" + minecraft_version
}

repositories {
    maven {
        name = "CurseMaven"
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        name = 'Sponge / Mixin'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    maven {
        name = "JourneyMap (Public)"
        url = "https://jm.gserv.me/repository/maven-public/"
    }

    maven {
        url "https://maven.shedaniel.me/"
    }

    maven {
        name = 'BlameJared Maven (CrT / Bookshelf)'
        url = 'https://maven.blamejared.com'
    }

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
    }

    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    /Fabric Seasons/
    modImplementation "curse.maven:fabric-seasons-413523:4576886"

    /Fabric Seasons Extras/
    modCompileOnly "curse.maven:fabric-seasons-extras-839878:4576902"

    /Xaero's Minimap/
    modCompileOnly "curse.maven:xaeros-minimap-263420:4593188"
//    modImplementation "curse.maven:xaeros-minimap-263420:4593188"

    /Xaero's Minimap Fair Play/
    modCompileOnly "curse.maven:xaeros-minimap-fair-play-edition-263466:4586337"
//    modImplementation "curse.maven:xaeros-minimap-fair-play-edition-263466:4586337"

    /Open Parties and Claims/
    modCompileOnly "curse.maven:open-parties-and-claims-636608:4663429"
//    modImplementation "curse.maven:open-parties-and-claims-636608:4663429"

    /FTB Chunks/
    modCompileOnly "curse.maven:ftb-library-fabric-438495:4596510"
    modCompileOnly "curse.maven:architectury-api-419699:4663009"
    modCompileOnly "curse.maven:ftb-teams-fabric-438497:4623115"
    modCompileOnly "curse.maven:ftb-chunks-fabric-472657:4654813"

//    modImplementation "curse.maven:ftb-library-fabric-438495:4596510"
//    modImplementation "curse.maven:architectury-api-419699:4663009"
//    modImplementation "curse.maven:ftb-teams-fabric-438497:4623115"
//    modImplementation "curse.maven:ftb-chunks-fabric-472657:4654813"

//    /JourneyMap/
//    modCompileOnly "curse.maven:journeymap-32274:4655321"
//    modImplementation "curse.maven:journeymap-32274:4655321"

    /Trinkets/
    modCompileOnly "dev.emi:trinkets:3.7.1"
//    modImplementation "dev.emi:trinkets:3.7.1"


    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    implementation 'com.electronwill.night-config:core:3.6.3'
    implementation 'com.electronwill.night-config:toml:3.6.3'

    modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.0"//source: https://github.com/Fuzss/forgeconfigapiport-fabric

    modApi group: 'info.journeymap', name: 'journeymap-api', version: project.journeymap_api_version_fabric, changing: true
    modCompileOnly  "curse.maven:journeymap-${project.jm_project_id}:${project.jm_file_id_fabric}"
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId base.archivesName.get()
            version project.version
            artifact jar
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

modrinth {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading+1)
    String recentChangelogText = changelogText.substring(firstHeading,nextHeading)

    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_fabric_id
    versionNumber = mod_version
    versionName = base.archivesName.get() + "-" + mod_version
    versionType = "release"
    uploadFile = jar

    List<String> mcVersions = modrinth_gameVersions.split(",")
    gameVersions = mcVersions

    changelog = recentChangelogText

    dependencies {
        required.project "fabric-seasons"
        optional.project "fabric-seasons-extras"
        required.project "forge-config-api-port"
        optional.project "journeymap"
        optional.project "trinkets"
        optional.project "xaeros-minimap"
        optional.project "xaeros-minimap-fair"
    }
}
