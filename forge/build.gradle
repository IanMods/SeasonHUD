plugins {
    id 'multiloader-loader'
    id "net.minecraftforge.gradle" version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'net.darkhax.curseforgegradle' version '1.1.16'
    id "com.modrinth.minotaur" version "2.+"
}

base {
    archivesName = "${mod_id}-forge-${minecraft_version}"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

jar {
    manifest {
        attributes([
                'MixinConfigs': "${mod_id}.mixins.json" + "," + "${mod_id}.forge.mixins.json",
        ])
    }
}

minecraft {
    mappings channel: 'official', version: minecraft_version
//    mappings channel: 'official', version: minecraft_version
    reobf = false
    copyIdeResources = true

    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at
    }

    runs {
        client {
            workingDirectory file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory file('runs/data')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            mods {
                modDataRun {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'

    //required for forgeconfigapiport because the transitive dep causes issues
    implementation "io.github.llamalad7:mixinextras-forge:0.3.5"


    api("fuzs.forgeconfigapiport:forgeconfigapiport-forge:${project.forgeconfigapiport_version}") {
        transitive = false
    }
    implementation("curse.maven:forge-config-api-port-fabric-547434:${forgeconfigapiport_forge_file_id}") {
        transitive = false
    }

    /Serene Seasons/
    implementation "curse.maven:serene-seasons-291874:${project.serene_seasons_forge_file_id}"
    implementation "com.github.glitchfiend:GlitchCore-forge:${minecraft_version}-${project.glitchcore_version}"

    /Curios/
    compileOnly "top.theillusivec4.curios:curios-forge:${project.curios_file_id}:api"
//    implementation "top.theillusivec4.curios:curios-forge:${project.curios_file_id}"
    compileOnly "curse.maven:accessories-938917:${project.accessories_forge_file_id}"
//    compileOnly "curse.maven:accessories-cc-layer-1005683:${project.cclayer_forge_file_id}"
    compileOnly "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"

    /Forge Config API Port/
    implementation "fuzs.forgeconfigapiport:forgeconfigapiport-forge:${project.forgeconfigapiport_version}"

    /Xaero's Minimap/
    compileOnly "curse.maven:xaeros-minimap-263420:${project.xaeros_forge_file_id}"

    /Xaero's Minimap Fair Play/
//    compileOnly "curse.maven:xaeros-minimap-fair-play-edition-263466:${project.xaeros_fairplay_forge_file_id}"

    /FTB Chunks/
    //FTB Library
    compileOnly "curse.maven:ftb-library-forge-404465:${project.ftb_lib_forge_file_id}"
    //Architectury
    compileOnly "curse.maven:architectury-api-419699:${project.architectury_forge_file_id}"
    //FTB Teams
    compileOnly "curse.maven:ftb-teams-forge-404468:${project.ftb_teams_forge_file_id}"
    //FTB Chunks
    compileOnly "curse.maven:ftb-chunks-forge-314906:${project.ftb_chunks_forge_file_id}"

    /JourneyMap/
    compileOnly "curse.maven:journeymap-32274:${project.journeymap_forge_file_id}"

    /Map Atlases/
    compileOnly("curse.maven:map-atlases-forge-519759:${project.map_atlases_forge_file_id}") {
        transitive = false
    }
//    compileOnly "curse.maven:selene-499980:5061472"
//    compileOnly "curse.maven:supplementaries-412082:5053567"
//    compileOnly "curse.maven:immediatelyfast-686911:4937545"
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        mavenJava(MavenPublication) {
            fg.component(it)
        }
    }
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

modrinth {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_forge_id
    versionNumber = minecraft_version + "-" + version
    versionName = base.archivesName.get() + "-" + mod_version
    versionType = "release"
    uploadFile = jar

    List<String> mcVersions = modrinth_gameVersions.split(",")
    gameVersions = mcVersions
    loaders = ["forge"]

    changelog = recentChangelogText

    dependencies {
        required.project "serene-seasons"
        required.project "forge-config-api-port"
        optional.project "curios"
        optional.project "journeymap"
        optional.project "xaeros-minimap"
        optional.project "xaeros-minimap-fair"
        optional.project "map-atlases"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_forge_id, jar)
    mainFile.changelogType = "markdown"
    mainFile.changelog = recentChangelogText
    mainFile.releaseType = "release"

    List<String> mcVersions = curseforge_gameVersions.split(",")
    mainFile.gameVersions = mcVersions
    mainFile.addGameVersion("Forge")
    mainFile.addGameVersion("Java 21")

    mainFile.addRequirement("serene-seasons")
    mainFile.addRequirement("forge-config-api-port-fabric")
    mainFile.addOptional("curios")
    mainFile.addOptional("ftb-chunks-forge")
    mainFile.addOptional("journeymap")
    mainFile.addOptional("xaeros-minimap")
    mainFile.addOptional("xaeros-minimap-fair-play-edition")
    mainFile.addOptional("map-atlases-forge")
}