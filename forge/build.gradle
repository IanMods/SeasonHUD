plugins {
    id 'com.github.johnrengelman.shadow'
    id 'net.darkhax.curseforgegradle' version '1.1.18'
    id "com.modrinth.minotaur" version "2.+"
}

loom {
    forge {
        mixinConfig "seasonhud.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    //Serene Seasons
    modImplementation "curse.maven:serene-seasons-291874:${project.serene_seasons_forge_version}"
    modImplementation "curse.maven:serene-seasons-291874:${project.serene_seasons_deobf_forge_version}"

    //Xaero's Minimap
    modCompileOnly "curse.maven:xaeros-minimap-263420:${project.xaeros_minimap_forge_version}"

    //JourneyMap
    modCompileOnly "curse.maven:journeymap-32274:${project.journeymap_forge_version}"

    //FTB Chunks + Dependencies
    modCompileOnly "curse.maven:ftb-chunks-forge-314906:${project.ftb_chunks_forge_version}"
    modCompileOnly "curse.maven:ftb-library-forge-404465:${project.ftb_library_forge_version}"
    modCompileOnly "curse.maven:ftb-teams-forge-404468:${project.ftb_teams_forge_version}"
    modCompileOnly "curse.maven:architectury-api-419699:${project.architectury_api_forge_version}"

    /Map Atlases/
//    modCompileOnly "curse.maven:map-atlases-forge-519759:${project.map_atlases_forge_version}"
//    modCompileOnly "curse.maven:selene-499980:4843863"
//    modCompileOnly "curse.maven:supplementaries-412082:4823730"

    //Curios + Accessories
    modCompileOnly "top.theillusivec4.curios:curios-forge:${project.curios_file_id}:api"

    // Development QOL
//    modLocalRuntime("mezz.jei:jei-${minecraft_version}-forge:${jei_version}") { transitive = false }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

modrinth {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_forge_id
    versionNumber = minecraft_version + "-" + version
    versionName = base.archivesName.get() + "-" + mod_version
    versionType = "release"
    uploadFile = jar

    List<String> mcVersions = modrinth_gameVersions.split(",")
    gameVersions = mcVersions
    loaders = ["forge"]

    changelog = recentChangelogText

    dependencies {
        required.project "serene-seasons"
        optional.project "curios"
        optional.project "journeymap"
        optional.project "xaeros-minimap"
        optional.project "xaeros-minimap-fair"
        optional.project "map-atlases"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_forge_id, jar)
    mainFile.changelogType = "markdown"
    mainFile.changelog = recentChangelogText
    mainFile.releaseType = "release"

    List<String> mcVersions = curseforge_gameVersions.split(",")
    mainFile.gameVersions = mcVersions
    mainFile.addGameVersion("Forge")
    mainFile.addGameVersion("Java 17")

    mainFile.addRequirement("serene-seasons")
    mainFile.addOptional("curios")
    mainFile.addOptional("ftb-chunks-forge")
    mainFile.addOptional("journeymap")
    mainFile.addOptional("xaeros-minimap")
    mainFile.addOptional("xaeros-minimap-fair-play-edition")
    mainFile.addOptional("map-atlases-forge")
}

task publishForge {
    group = "publishing"
    afterEvaluate {
        dependsOn(tasks.publishCurseForge)
        dependsOn(tasks.modrinth)
    }
}