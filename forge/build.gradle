plugins {
    id 'com.github.johnrengelman.shadow'
    id 'net.darkhax.curseforgegradle'
    id "com.modrinth.minotaur"
}

loom {
    forge {
        mixinConfig "seasonhud.mixins.json"
        mixinConfig "seasonhud.forge.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }

    configureEach {
        resolutionStrategy.force("net.sf.jopt-simple:jopt-simple:5.0.4")
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    //Serene Seasons
    modImplementation "maven.modrinth:serene-seasons:${project.serene_seasons_version}-forge"
    modImplementation "maven.modrinth:glitchcore:${project.glitchcore_version}-forge"

    //Xaero's Minimap
    modCompileOnly "maven.modrinth:xaeros-minimap:${project.xaeros_minimap_version}_Forge_1.21"

    //JourneyMap
    modCompileOnlyApi group: 'info.journeymap', name: 'journeymap-api-forge', version: project.journeymap_api_version_fabric, changing: true
    modCompileOnly "maven.modrinth:journeymap:1.21-${project.journeymap_version}+forge"

    //Forge Config API Port
    modImplementation "maven.modrinth:forge-config-api-port:v${project.forge_config_api_port_version}-${project.minecraft_version}-Forge"


    // Development QOL
//    modLocalRuntime("mezz.jei:jei-${project.minecraft_version}-forge:${project.jei_version}") { transitive = false }

    // Minimap Mod Dev Tests
    // See root gradle.properties to choose which to use at runtime.
    switch (project.minimap_mod.toLowerCase(Locale.ROOT)) {
        case "xaero":
            modLocalRuntime "maven.modrinth:xaeros-minimap:${project.xaeros_minimap_version}_Forge_1.21"
            break
        case "journey":
            modLocalRuntime "maven.modrinth:journeymap:1.21-${project.journeymap_version}+forge"
            break
        case "ftb":
            break
        case "atlas":
            break
        case "none":
            break
        default: println("Unknown recipe viewer specified: ${project.minimap_mod}. Must be xaero, journey, ftb, " +
                "atlas, or none")
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

modrinth {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_forge_id
    versionNumber = minecraft_version + "-" + version
    versionName = base.archivesName.get() + "-" + mod_version
    versionType = "release"
    uploadFile = remapJar

    List<String> mcVersions = modrinth_gameVersions.split(",")
    gameVersions = mcVersions
    loaders = ["forge"]

    changelog = recentChangelogText

    dependencies {
        required.project "serene-seasons"
        required.project "forge-config-api-port"
        optional.project "curios"
        optional.project "accessories"
        optional.project "xaeros-minimap"
        optional.project "xaeros-minimap-fair"
        optional.project "journeymap"
        optional.project "map-atlases"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "curseforge"

    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_forge_id, remapJar)
    mainFile.changelogType = "markdown"
    mainFile.changelog = recentChangelogText
    mainFile.releaseType = "release"

    List<String> mcVersions = curseforge_gameVersions.split(",")
    mainFile.gameVersions = mcVersions
    mainFile.addGameVersion("Forge")
    mainFile.addJavaVersion("Java 21")

    mainFile.addRequirement("serene-seasons")
    mainFile.addRequirement("forge-config-api-port-fabric")
    mainFile.addOptional("curios")
    mainFile.addOptional("accessories")
    mainFile.addOptional("xaeros-minimap")
    mainFile.addOptional("xaeros-minimap-fair-play-edition")
    mainFile.addOptional("ftb-chunks-forge")
    mainFile.addOptional("journeymap")
    mainFile.addOptional("map-atlases-forge")
}

task publishForge {
    group = "upload"
    afterEvaluate {
        dependsOn(tasks.publishCurseForge)
        dependsOn(tasks.modrinth)
    }
}